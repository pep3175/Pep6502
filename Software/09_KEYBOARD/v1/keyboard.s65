    .setcpu "6502"
    .include "zeropage.inc65"
    .include "io.inc65"
    .include "macros.inc65"
    .include "acia.inc65"

    .export keyboard_init
    .export keyboard_interrupt

    .bss
    KEYBOARD_BUFFER_LENGTH = 256
keyboard_buffer:
    .res KEYBOARD_BUFFER_LENGTH + 1, 0

    .code
    RELEASE = %00000001
    SHIFT   = %00000010

keyboard_init:
    lda #%10000010
    sta VIA1_IER
    lda #%00000001
    sta VIA1_PCR
    ; clear interrupt
    cli
    lda #%00000000
    sta VIA1_DDRA
    lda #$00
    sta kb_flags
    sta kb_wptr
    sta kb_rptr
    rts

keyboard_interrupt:
    phax
    lda kb_flags
    and #RELEASE   ; check if we're releasing a key
    beq read_key   ; otherwise, read the key

    lda kb_flags
    eor #RELEASE   ; flip the releasing bit
    sta kb_flags
    lda VIA1_IRA      ; read key value that's being released
    cmp #$12       ; left shift
    beq shift_up
    cmp #$59       ; right shift
    beq shift_up
    jmp exit

shift_up:
    lda kb_flags
    eor #SHIFT  ; flip the shift bit
    sta kb_flags
    jmp exit

read_key:
    lda VIA1_IRA
    cmp #$f0        ; if releasing a key
    beq key_release ; set the releasing bit
    cmp #$12        ; left shift
    beq shift_down
    cmp #$59        ; right shift
    beq shift_down

    tax
    lda kb_flags
    and #SHIFT
    bne shifted_key

    lda keymap, x   ; map to character code
    jmp push_key

shifted_key:
    lda keymap_shifted, x   ; map to character code

push_key:
    ;ldx kb_wptr
    ;sta kb_buffer, x
    ;inc kb_wptr
    jsr acia_putc
    jmp exit

shift_down:
    lda kb_flags
    ora #SHIFT
    sta kb_flags
    jmp exit

key_release:
    lda kb_flags
    ora #RELEASE
    sta kb_flags

exit:
    plax
    rti


  .rodata
keymap:
    .byte "????????????? `?" ; 00-0F
    .byte "?????q1???zsaw2?" ; 10-1F
    .byte "?cxde43?? vftr5?" ; 20-2F
    .byte "?nbhgy6???mju78?" ; 30-3F
    .byte "?,kio09??./l;p-?" ; 40-4F
    .byte "??'?[=????",$0a,"]?\??" ; 50-5F
    .byte "??????",$0b,"??1?47???" ; 60-6F
    .byte "0.2568",$1b,"??+3-*9??" ; 70-7F
    .byte "????????????????" ; 80-8F
    .byte "????????????????" ; 90-9F
    .byte "????????????????" ; A0-AF
    .byte "????????????????" ; B0-BF
    .byte "????????????????" ; C0-CF
    .byte "????????????????" ; D0-DF
    .byte "????????????????" ; E0-EF
    .byte "????????????????" ; F0-FF
keymap_shifted:
    .byte "????????????? ~?" ; 00-0F
    .byte "?????Q!???ZSAW@?" ; 10-1F
    .byte "?CXDE#$?? VFTR%?" ; 20-2F
    .byte "?NBHGY^???MJU&*?" ; 30-3F
    .byte "?<KIO)(??>?L:P_?" ; 40-4F
    .byte "??'?{+?????}?|??" ; 50-5F
    .byte "?????????1?47???" ; 60-6F
    .byte "0.2568???+3-*9??" ; 70-7F
    .byte "????????????????" ; 80-8F
    .byte "????????????????" ; 90-9F
    .byte "????????????????" ; A0-AF
    .byte "????????????????" ; B0-BF
    .byte "????????????????" ; C0-CF
    .byte "????????????????" ; D0-DF
    .byte "????????????????" ; E0-EF
    .byte "????????????????" ; F0-FF